grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Program:
	'program' name=ID
	externals+=External*
	mathExps=MathExp
;

External:
	'external' name=ID '(' (args+='int'(','args+='int')*)? ')'
;

Method:
	ref = [External] '(' (exps+=Exp (','exps+=Exp)*)? ')'
;

MathExp:
	variables += VarBinding*
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | {Parenthesis} '(' exp=Exp ')'  | VariableUse | LetBinding | Method
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
